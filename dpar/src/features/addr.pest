WHITESPACE = _{" " | "\t" | "\r" | "\n" }

sep = @{ (" " | "\t" )+ }

source_idx = ${ ASCII_DIGIT+ }

// Parser state data structures.
stack = @{ "STACK" ~sep~ source_idx }
buffer = @{ "BUFFER" ~sep~ source_idx }
ldep = @{ "LDEP" ~sep~ source_idx }
rdep = @{ "RDEP" ~sep~ source_idx }

initial_source = { (stack | buffer) }
dep_source = { (ldep | rdep) }

layer = { (char_layer | deprel_layer | feature_layer | tag_layer | token_layer ) }

// Layers
char_layer = @{ "CHAR" ~ sep ~ affix_len ~ sep ~ affix_len }
deprel_layer = { "DEPREL" }
feature_layer = @{ "FEATURE" ~ sep ~ feature_name }
tag_layer = { "TAG" }
token_layer = { "TOKEN" }

// Layer arguments.
feature_name = ${ ASCII_ALPHANUMERIC+ }
affix_len = ${ ASCII_DIGIT+ }

addr = { "[" ~ initial_source ~ ("," ~ dep_source)* ~ "]" }

addr_value = { addr ~ layer }

file = { SOI ~ addr_value* ~ EOI }
